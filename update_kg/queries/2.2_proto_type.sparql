insert {GRAPH <http://www.isi.com/supergraph> {
    [] a rdf:Statement ;
        rdf:subject ?prototype ;
        rdf:predicate rdf:type ;
        rdf:object ?t
    }
}
where {
    select ?prototype (max(?type1) as ?t)
    where {
      {
        select ?prototype (max(?cnt) as ?max)
        where {
          select ?prototype ?type (count(?type) as ?cnt)
          where {
            GRAPH <http://www.isi.com/supergraph> {
                ?c aida:prototype ?prototype .
                ?membership aida:cluster ?c ; aida:clusterMember ?e .
            }
            ?x a rdf:Statement ; rdf:subject ?e ; rdf:predicate rdf:type ; rdf:object ?type .
          } group by ?prototype ?type
        } group by ?prototype
      }
      {
        select ?prototype ?type1 (count(?type1) as ?max)
        where {
            GRAPH <http://www.isi.com/supergraph> {
                ?c1 aida:prototype ?prototype .
                ?membership1 aida:cluster ?c1 ; aida:clusterMember ?e1 .
            }
            ?x a rdf:Statement ; rdf:subject ?e1 ; rdf:predicate rdf:type ; rdf:object ?type1 .
        } group by ?prototype ?type1
      }
    } group by ?prototype
}
